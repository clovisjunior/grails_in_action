buildscript {
    repositories {
        maven { url "http://repo.grails.org/grails/core" }
    }

    dependencies {
        classpath "org.grails:grails-gradle-plugin:2.0.1"
    }
}

subprojects {
    if (project.file("grails-app").exists()) {

        apply plugin: "grails"

        repositories {
            grails.central()
        }

        dependencies {
            compile "org.grails.plugins:hibernate:3.6.10.6", {
                exclude module: "ehcache-core"
            }

            bootstrap "org.grails.plugins:tomcat:7.0.47"
        }

        grails {
            grailsVersion = "2.3.7"
            groovyVersion = "2.1.9"
            springLoadedVersion = "1.1.4"
        }

        tasks.withType(org.grails.gradle.plugin.tasks.GrailsTask) { Task t ->
            t.jvmOptions {
                jvmArgs "-Xmx512m", "-XX:MaxPermSize=256m"
            }
        }
    }

    group = "com.grailsinaction"
    version = "1.0-SNAPSHOT"
}
