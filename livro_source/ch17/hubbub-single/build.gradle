buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.grails:grails-gradle-plugin:2.0.1"
    }
}

apply plugin: "grails"

repositories {
    grails.central()
    mavenLocal()
//    maven { url "file:///${System.getProperty('user.home')}/.m2/repository" }
}

dependencies {
    bootstrap "org.grails.plugins:tomcat:7.0.47"
    compile "org.apache.activemq:activemq-core:5.7.0", {
        exclude module: "spring-context"
    }

    compile "org.apache.lucene:lucene-spellchecker:2.4.1"

    compile "org.grails.plugins:hibernate:3.6.10.6", {
        exclude module: "ehcache-core"
    }

    runtime "org.grails.plugins:jquery:1.10.2.2"
    runtime "org.grails.plugins:resources:1.2.1"
    runtime "org.grails.plugins:navigation:1.3.2"

    compile "org.grails.plugins:database-migration:1.3.8"

    compile 'org.grails.plugins:cache:1.1.1', "org.grails.plugins:cache-ehcache:1.0.1"
    compile "org.grails.plugins:mail:1.0.1"
    compile "org.grails.plugins:searchable:0.6.6"
    compile "org.grails.plugins:spring-security-core:2.0-RC2", "org.grails.plugins:spring-security-ui:1.0-RC1"
    compile "org.grails.plugins:spring-security-twitter:0.6.2"
    compile "org.grails.plugins:jms:1.3"
    compile "org.grails.plugins:quartz:1.0.2"

    compile "org.grails.plugins:platform-core:1.0.0"

    test "org.grails.plugins:dumbster:0.2"

    def gebVersion = "0.9.2"
    def seleniumVersion = "2.41.0"
    test "org.gebish:geb-spock:$gebVersion"
    test "com.github.groovy-wslite:groovy-wslite:0.7.2"

    test "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
    test "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
    test "org.grails.plugins:geb:$gebVersion"
}


group = "com.grailsinaction"
version = "1.0-SNAPSHOT"

grails {
    grailsVersion = "2.3.7"
    groovyVersion = "2.1.9"
    springLoadedVersion "1.1.5.RELEASE"
}

task stdTest(type: org.grails.gradle.plugin.tasks.GrailsTask) {
    env = "test"
    command = "test-app"
    args = "unit: integration:"
}

tasks.'grails-war'.mustRunAfter stdTest
assemble.dependsOn stdTest

tasks.withType(org.grails.gradle.plugin.tasks.GrailsTask) { Task t ->
    t.jvmOptions {
        jvmArgs "-Xmx384m", "-XX:MaxPermSize=256m"
    }
}
